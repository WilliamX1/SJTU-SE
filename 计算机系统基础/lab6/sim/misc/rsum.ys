# Huidong Xu
# 519021910861
# Execution begins at address 0
	.pos 0
	irmovq stack, %rsp	# Set up stack pointer
	call main		# Execution main program
	halt			# Terminate program

# Sample linked list
	.align 8
ele1:
	.quad 0x00a
	.quad ele2
ele2: 	
	.quad 0x0b0
	.quad ele3
ele3:
	.quad 0xc00
	.quad 0

main: 	irmovq ele1, %rdi
	call rsum_list
	ret

# long rsum_list(list_ptr ls)
# ls in %rdi
rsum_list:	irmovq $8, %r8		# Constant 8
		xorq %rax, %rax		# rax = 0
		andq %rdi, %rdi		# test !ls
		je	return		# if (!ls) return
		mrmovq (%rdi), %rbx	# val = ls->val
		pushq %rbx		# Save %rbx
		addq %r8, %rdi
		mrmovq (%rdi), %rdi	# ls = ls->next
		call rsum_list
		popq %rbx		# get val
		addq %rbx, %rax		# return val + rest
return:
		ret

# Stack starts here and grows to lower addresses
	.pos 0x200
stack:
